buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1"
  }
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// call the bintray target to publish to bintray
defaultTasks 'clean', 'build', 'jacocoTestReport'


group = 'coyote'
version = '0.1'


ext {
	bintray_package = 'Commons'
}

jar {
    manifest {
        attributes 'Provider': 'Coyote Codeworks', 
        'Implementation-Version': version
    }
}


// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}


// add javadoc/source jar tasks as artifacts
artifacts {
	archives sourcesJar, javadocJar
}


repositories {
    jcenter()
}


ext.junitVersion = '4.11'
ext.servletVersion = '2.5'
ext.h2Version = '1.4.177'

dependencies {
	// jUnit support for testing
	testCompile "junit:junit:$junitVersion"
	
	testCompile "com.h2database:h2:$h2Version"

	// Servlet support
	compile "javax.servlet:servlet-api:$servletVersion"
	runtime "javax.servlet:servlet-api:$servletVersion"
    
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			if (plugins.hasPlugin('war')) {
				from components.web
			} else {
				from components.java
			}

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
}


bintray {
	user = bintrayUser
	key = bintrayKey
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'Commons'
		desc = 'A collection of useful utility classes common to many different projects.'
		licenses = ['MIT']
		labels = ['Java', 'util', 'commons']
	}
}


// Used to share projects locally
uploadArchives {
    repositories {
		ivy {
			url "../local-repo"
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}
 